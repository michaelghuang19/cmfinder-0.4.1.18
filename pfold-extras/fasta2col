#!/bin/gawk -f

BEGIN{ print "; generated by fasta2col";
       print "; ============================================================";
}
/^>/{ if(NR>1) print "; **********";
      k=1;
      sp = 1;
      print "; TYPE              arbitrary";
      print "; COL 1             label";
      print "; COL 2             residue";
      print "; COL 3             seqpos";
      print "; COL 4             alignpos";
      print "; ENTRY             "substr($1,2);
      print "; ----------";
}

!/^>/{ tlen=length($0);
       for(i=1;i<tlen+1;i++) {
	 if (substr($0,i,1) == "-") {
	   printf("G  %c      .  %5d\n", substr($0,i,1), k++)
	     }
	 else {
	   printf("N  %c  %5d  %5d\n", substr($0,i,1), sp++, k++)
	     }
       }
}

END{ print "; **********"; }


########## end of file ###########
